#!/usr/bin/env coffee

require './generate'

mkdir "data/meta"
mkdir "data/name"
mkdir "data/tags"

for input_file in process.argv[2..]
  id = input_file.replace /^test\/(.*)\.tml$/, '$1'
  print "\r#{id}"
  testml = read input_file
  ast = (new TestMLCompiler.Compiler).compile(testml)
  data = JSON.parse(ast).data[0]
  {label, point} = data
  tags = point.tags.split /\ +/

  mkdir "data/#{id}"
  write "data/#{id}/===", "#{label}\n"

  name = _.lowerCase label
  name = name.replace /[^-\w]+/g, '-'
  name = name.replace /^-?(.*?)-?$/, '$1'
  link "data/name/#{name}", "../#{id}"

  mkdir "data/meta/#{id}"
  link "data/meta/#{id}.label", label

  for tag in tags
    mkdir "data/tags/#{tag}" unless exists "data/tags/#{tag}"
    link "data/tags/#{tag}/#{id}", "../../#{id}"

  for name in [
    'in-yaml',
    'out-yaml',
    'emit-yaml',
    'in-json',
    'test-event',
    'lex-token',
    'error',
  ]
    if (text = point[name])?
      file = name.replace /^(.*)-(.*)$/, '$1.$2'
      if name.match /^(in-yaml|out-yaml|test-event|emit-yaml)$/
        text = text.replace /<SPC>/g, ' '
        text = text.replace /<TAB>/g, '\t'
      write "data/#{id}/#{file}", text

say '\rDone'
